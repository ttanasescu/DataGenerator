expression = term , "|" , expression
		| term ;

term = { factor } ;

factor = base , { "*" }
		| base , { "+" }
		| base , { "?" }
		| base , "{" , quantifier , "}" ;

quantifier = number
		| number , ","
		| number , "," , number ;

number = digit , { digit } ;

base = "."
		| char
		| "\" , char
		| pick 
		| "(" , expression , ")";

pick = "[" , [ "^" ] , { basic | range } , "]";

basic = char
		| "\" , char ;
		
range = letter , "-" , letter
		| digit , "-" , digit ;

char = letter | digit | symbol | "_" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
		| "H" | "I" | "J" | "K" | "L" | "M" | "N"
		| "O" | "P" | "Q" | "R" | "S" | "T" | "U"
		| "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
		| "c" | "d" | "e" | "f" | "g" | "h" | "i"
		| "j" | "k" | "l" | "m" | "n" | "o" | "p"
		| "q" | "r" | "s" | "t" | "u" | "v" | "w"
		| "x" | "y" | "z" ;

symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
		| "'" | '"' | "=" | "|" | "." | "," | ";" ;


